import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext {
        kotlin_version = '1.9.22'
        ksp_version = '1.9.22-1.0.17'
        kotlin_coroutines_version = "1.7.3"
        compose_version = "1.7.0-alpha01"
        compose_compiler_version = "1.5.8"
        koin_version = "3.5.3"
        okhttp = "4.11.0"
        ktor = "1.5.0"
        moshi = "1.15.0"
        coil = "2.5.0"
        room = "2.6.1"
        joda_time = "2.12.5"
        jsoup = "1.14.3"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.0.2'
        classpath 'com.google.gms:google-services:4.4.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "com.google.devtools.ksp:symbol-processing-gradle-plugin:${ksp_version}"
    }
}

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("kuroba_ex_lite.enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
            }
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}